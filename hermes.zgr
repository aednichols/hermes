grammar {
  lexer<python> {
    partials {
      r'\#.*' -> _comment
      r'\s+' -> _ws
    }

    r'(grammar)\s*({)' -> :grammar[1] :lbrace[2] @grammar
    r'{%_ws%}' -> null
    r'{%_comment%}' -> null

    mode<grammar> {
      r'{%_ws%}' -> null
      r'{%_comment%}' -> null
      r'}' -> :rbrace %pop
      r'(lexer)\s*(<)\s*([a-zA-Z]+)\s*(>)\s*({)'
          -> :lexer[1] :langle[2] :identifier[3] :rangle[4] :lbrace[5] @lexer
      r'(parser\s*<\s*ll1\s*>)\s*({)'
          -> :parser_ll1[1] :lbrace[2] @parser_ll1
    }

    mode<lexer> {
      r'{%_ws%}' -> null
      r'{%_comment%}' -> null
      r'}' -> :rbrace %pop
      r'null' -> :null
      r'\(' -> :lparen
      r'\)' -> :rparen
      r'\[\]' -> :no_group
      r'\[' -> :lsquare
      r'\]' -> :rsquare
      r'[0-9]+' -> :integer
      r'r\'(\\\'|[^\'])*\'' -> :regex
      r'"(\\\"|[^\"])*"' -> :regex
      r'->' -> :arrow
      r'@([a-zA-Z][a-zA-Z0-9_]+)' -> :stack_push[1]
      r'%([a-zA-Z][a-zA-Z0-9_]+)' -> :action[1]
      r':([a-zA-Z][a-zA-Z0-9_]*|_empty)' -> :terminal[1]
      r'(mode)\s*(<)\s*([a-zA-Z0-9_]+)\s*(>)\s*({)'
          -> :mode[1] :langle[2] :identifier[3] :rangle[4] :lbrace[5] @lexer
      r'(partials)\s*({)'
          -> :partials[1] :lbrace[2] @partials
      r'[a-zA-Z][a-zA-Z0-9_]*' -> :identifier
      r'<code>(.*?)</code>' {DOTALL} -> :code[1]
    }

    mode<partials> {
      r'{%_ws%}' -> null
      r'{%_comment%}' -> null
      r'r\'(\\\'|[^\'])*\'' -> :regex
      r'"(\\\"|[^\"])*"' -> :regex
      r'->' -> :arrow
      r'_([a-zA-Z][a-zA-Z0-9_]*)' -> :regex_partial
      r'}' -> :rbrace %pop
    }

    mode<parser_ll1> {
      r'{%_ws%}' -> null
      r'{%_comment%}' -> null
      r'}' -> :rbrace %pop
      r'\|' -> :pipe
      r'=' -> :equals
      r'\(' -> :lparen
      r'\)' -> :rparen
      r',' -> :comma
      r'->' -> :arrow
      r'parser\s*<\s*expression\s*>\s*({)' -> :parser_expression[] :lbrace[1] @parser_expr
      r':([a-zA-Z][a-zA-Z0-9_]*|_empty)' -> :terminal[1]
      r'\$([a-zA-Z][a-zA-Z0-9_]*)(?=\s*\=)' -> :ll1_rule_hint[] :nonterminal[1]
      r'\$([a-zA-Z][a-zA-Z0-9_]*)' -> :nonterminal[1]
      r'\$([0-9]+|\$)' -> :nonterminal_reference[1]
      r'[a-zA-Z][a-zA-Z0-9_]*' -> :identifier
      r'"[^"]+"' -> :string
      r'[0-9]+' -> :integer
    }

    mode<parser_expr> {
      r'{%_ws%}' -> null
      r'{%_comment%}' -> null
      r'(\()(?=\s*[\*-])' -> :lparen[1] @binding_power
      r'->' -> :arrow
      r'<=>' -> :expression_divider
      r'\|' -> :pipe
      r'=' -> :equals
      r'}' -> :rbrace %pop
      r'\(' -> :lparen
      r'\)' -> :rparen
      r',' -> :comma
      r':([a-zA-Z][a-zA-Z0-9_]*|_empty)' -> :terminal[1]
      r'(\$([a-zA-Z][a-zA-Z0-9_]*))[ \t]*(=)[ \t]*\1[ \t]+:([a-zA-Z][a-zA-Z0-9_]*)[ \t]+\1(?![ \t]+(:|\$))'
          -> :expr_rule_hint[] :nonterminal[2] :equals[3] :infix_rule_hint[] :nonterminal[2] :terminal[4] :nonterminal[2]
      r'(\$([a-zA-Z][a-zA-Z0-9_]*))[ \t]*(=)[ \t]*:([a-zA-Z][a-zA-Z0-9_]*)[ \t]+\1(?![ \t](:|\$))'
          -> :expr_rule_hint[] :nonterminal[2] :equals[3] :prefix_rule_hint[] :terminal[4] :nonterminal[2]
      r'\$([a-zA-Z][a-zA-Z0-9_]*)\s*(=)'
          -> :expr_rule_hint[] :nonterminal[1] :equals[2] :mixfix_rule_hint[]
      r'\$([a-zA-Z][a-zA-Z0-9_]*)' -> :nonterminal[1]
      r'\$([0-9]+|\$)' -> :nonterminal_reference[1]
      r'[a-zA-Z][a-zA-Z0-9_]*' -> :identifier
      r'"[^"]+"' -> :string
      r'[0-9]+' -> :integer
    }

    mode<binding_power> {
      r'{%_ws%}' -> null
      r'\*' -> :asterisk
      r'-' -> :dash
      r':' -> :colon
      r'left' -> :left
      r'right' -> :right
      r'unary' -> :unary
      r'\)' -> :rparen %pop
    }
  }

  parser<ll1> {
    $grammar = :grammar :lbrace list($body_element) :rbrace -> Grammar(body=$2)
    $body_element = $body_element_sub -> $0
    $body_element_sub = $lexer | $parser
    $lexer = :lexer :langle :identifier :rangle :lbrace list($lexer_atom) optional(:code) :rbrace -> Lexer(language=$2, atoms=$5, code=$6)
    $lexer_atom = $lexer_regex | $lexer_mode | $lexer_partials
    $lexer_partials = :partials :lbrace list($regex_partial) :rbrace -> RegexPartials(list=$2)
    $regex_partial = :regex :arrow :regex_partial -> RegexPartial(regex=$0, name=$2)
    $lexer_regex = :regex optional($regex_options) :arrow list($lexer_target) -> Regex(regex=$0, options=$1, onmatch=$3)
    $regex_options = :lbrace list(:identifier) :rbrace -> $1
    $lexer_target = $terminal | :identifier :lparen optional($terminal) :rparen -> LexerFunctionCall(name=$0, terminal=$2)
    $lexer_target = $terminal | :stack_push | :action
    $terminal = :terminal optional($match_group) -> Terminal(name=$0, group=$1)
    $match_group = :lsquare :integer :rsquare -> $1
    $match_group = :no_group
    $lexer_target = :null -> Null()
    $lexer_mode = :mode :langle :identifier :rangle :lbrace list($lexer_atom) :rbrace -> Mode(name=$2, atoms=$5)
    $parser = $parser_ll1 | $parser_expression
    $parser_ll1 = :parser_ll1 :lbrace list($ll1_rule) :rbrace -> Parser(rules=$2)
    $ll1_rule = :ll1_rule_hint :nonterminal :equals $ll1_rule_rhs -> Rule(nonterminal=$1, production=$3)
    $ll1_rule_rhs = list($rule, :pipe)
    $rule = list($morpheme) optional($ast_transform) -> Production(morphemes=$0, ast=$1)
    $ll1_rule_rhs = :null -> NullProduction()
    $ll1_rule_rhs = $parser
    $parser_expression = :parser_expression :lbrace list($expression_rule) :rbrace -> ExpressionParser(rules=$2)
    $expression_rule = optional($binding_power) :expr_rule_hint :nonterminal :equals $expression_rule_production -> ExpressionRule(precedence=$0, nonterminal=$2, production=$4)
    $expression_rule_production = :mixfix_rule_hint $nud optional($ast_transform) optional($led) optional($ast_transform) -> MixfixProduction(nud=$1, nud_ast=$2, led=$3, ast=$4)
    $expression_rule_production = :prefix_rule_hint list($morpheme) optional($ast_transform) -> PrefixProduction(morphemes=$1, ast=$2)
    $expression_rule_production = :infix_rule_hint list($morpheme) optional($ast_transform) -> InfixProduction(morphemes=$1, ast=$2)
    $nud = list($morpheme)
    $led = :expression_divider list($morpheme) -> $1
    $binding_power = :lparen $precedence :rparen -> $1
    $precedence = $binding_power_marker :colon $associativity -> Precedence(marker=$0, associativity=$2)
    $binding_power_marker = :asterisk | :dash
    $associativity = :left | :right | :unary
    $morpheme = :terminal | :nonterminal | $macro
    $ast_transform = :arrow $ast_transform_sub -> $1
    $ast_transform_sub = :identifier :lparen list($ast_parameter, :comma) :rparen -> AstTransformation(name=$0, parameters=$2)
    $ast_transform_sub = :nonterminal_reference
    $ast_parameter = :identifier :equals :nonterminal_reference -> AstParameter(name=$0, index=$2)
    $macro = :identifier :lparen list($macro_parameter, :comma) :rparen -> Macro(name=$0, parameters=$2)
    $macro_parameter = :nonterminal | :terminal | :string | :integer
  }
}
